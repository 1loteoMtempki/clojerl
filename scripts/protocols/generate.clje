(ns protocols.generate
  (:require [protocols.utils :as utils]
            [clojure.string :as str]))

(def primitive-types
  '{"clojerl.String"    {:pred is_binary
                         :order 1}
    "clojerl.BitString" {:pred is_bitstring
                         :order 2}
    "clojerl.Integer"   {:pred is_integer
                         :order 3}
    "clojerl.Float"     {:pred is_float
                         :order 4}
    "clojerl.Boolean"   {:pred is_boolean
                         :order 5}
    "erlang.List"       {:pred is_list
                         :order 6}
    "erlang.Map"        {:pred is_map
                         :order 7}
    "erlang.Tuple"      {:pred is_tuple
                         :order 8}
    "erlang.Fn"         {:pred is_function
                         :order 9}
    ;; nil doesn't implement any protocols
    ;; "clojerl.Nil"       {:pred nil
    ;;                      :order 10}
    "clojerl.Keyword"   {:pred is_atom
                         :order 11}
    "erlang.Port"       {:pred is_port
                         :order 12}
    "erlang.Process"    {:pred is_pid
                         :order 13}
    "erlang.Reference"  {:pred is_reference
                         :order 14}})

(def primitive-type? primitive-types)

(def protocols
  '{clojerl.IAssociative   [contains_key  [Assoc Key]
                            entry_at      [Assoc Key]
                            assoc         [Assoc Key Value]]
    clojerl.IChunk         [drop_first    [Chunk]]
    clojerl.IChunkedSeq    [chunked_first [Seq]
                            chunked_next  [Seq]
                            chunked_more  [Seq]]
    clojerl.IColl          [cons          [Coll, Item]
                            empty         [Coll]]
    clojerl.ICounted       [count         [Seq]]
    clojerl.IDeref         [deref         [Ref]]
    clojerl.IEquiv         [equiv         [X Y]]
    clojerl.IErl           [->erl         [X Recursive]]
    clojerl.IError         [message       [Error]]
    clojerl.IExceptionInfo [data          [ExInfo]
                            cause         [ExInfo]]
    clojerl.IFn            [apply         [Fn Args]]
    clojerl.IHash          [hash          [X]]
    clojerl.IIndexed       [nth           [Coll N]
                            nth           [Coll N NotFound]]
    clojerl.ILookup        [get           [X Key]
                            get           [X Key NotFound]]
    clojerl.IMap           [keys          [Map]
                            vals          [Map]
                            without       [Map Key]]
    clojerl.IMeta          [meta          [X]
                            with_meta     [X Meta]]
    clojerl.INamed         [name          [X]
                            namespace     [X]]
    clojerl.IRecord        [_             [_]]
    clojerl.IReduce        [reduce        [Coll Fun]
                            reduce        [Coll Fun Init]]
    clojerl.IReference     [alter_meta    [Ref Fun Args]
                            reset_meta    [Ref Meta]]
    clojerl.IReversible    [rseq          [Seq]]
    clojerl.ISeq           [first         [Seq]
                            next          [Seq]
                            more          [Seq]]
    clojerl.ISeqable       [seq           [X]
                            to_list       [X]]
    clojerl.ISequential    [_             [_]]
    clojerl.ISet           [disjoin       [Coll Item]
                            contains      [Coll Item]]
    clojerl.ISorted        [_             [_]]
    clojerl.IStack         [peek          [Stack]
                            pop           [Stack]]
    clojerl.IStringable    [str           [X]]
    clojerl.IType          [_             [_]]

    ;; Erlang IO protocols
    erlang.io.IWriter         [write     [Writer Str]
                               write     [Writer Format Value]]
    erlang.io.IReader         [read      [Reader]
                               read      [Reader Length]
                               read_line [Reader]
                               skip      [Reader N]]
    erlang.io.IPushbackReader [unread    [Reader Ch]]
    erlang.io.ICloseable      [close     [X]]
    })

(defn underscore?
  [x]
  (= x '_))

(defn keyword-type?
  [t]
  (= t "clojerl.Keyword"))

(defn proto-function-head
  [f args]
  (let [args-str (apply str (interpose ", " args))]
    (str "'" f "'(" args-str ") ->\n"
         "  case " (first args) " of\n")))

(defn pattern-and-guard
  [m]
  (if (primitive-type? m)
    (str "ZZZ when " (get-in primitive-types [m :pred]) "(ZZZ)")
    (str "#{?TYPE := '" m "'}")))

(defn not-implemented [f args]
  (str "      clj_protocol:not_implemented(?MODULE, '" f "', " (first args) ")"))

(defn implementation-call [f args type]
  (let [args-str (apply str (interpose ", " args))]
    (str "    " (pattern-and-guard type) " ->\n"
         "      '" type "':'" f "'(" args-str ");\n")))

(defn proto-function-tail
  [f args]
  (str "    _ ->\n"
       (not-implemented f args) "\n"
       "  end.\n\n"))

(defn proto-function-clause
  [f args type]
  (cond
    ;; Catch all clause for tagged maps
    (underscore? type)
    (str "    #{?TYPE := _} ->\n"
         (not-implemented f args) ";\n")
    ;; Add a clause for nil before the one for keywords
    (= type "clojerl.Keyword")
    (str "    ?NIL ->\n"
         (not-implemented f args) ";\n"
         (implementation-call f args type))
    :else
    (implementation-call f args type)))

(defn proto-function
  [proto-name f args types]
  (when-not (underscore? f)
    (str
     (proto-function-head f args)
     (->> types
          (map (partial proto-function-clause f args))
          (apply str))
     (when-not (some keyword-type? types)
       (str "    ?NIL ->\n"
            (not-implemented f args) ";\n"))
     (proto-function-tail f args))))

(defn proto-functions
  [proto-name fns types]
  (->> fns
       (partition 2)
       (map (fn [[f args]]
              (proto-function proto-name f args types)))
       (apply str)))

(defn satisfies?-clause
  [type]
  (cond
    (underscore? type)
    "    #{?TYPE := _} -> false;\n"

    (keyword-type? type)
    (str "    ?NIL -> false;\n"
         "    " (pattern-and-guard type) " -> true;\n")

    :else
    (str "    " (pattern-and-guard type) " -> true;\n")))

(defn satisfies?-function
  [types]
  (str "?SATISFIES(X) ->\n"
       "  case X of\n"
       (->> types
            (map satisfies?-clause)
            (apply str))
       (when-not (some keyword-type? types)
         "    ?NIL -> false;\n")
       "    _ -> false\n"
       "  end.\n"))

(defn exports
  [fns]
  (->> fns
       (partition 2)
       (keep (fn [[f args]]
               (when-not (underscore? f)
                 (str "'" f "'/" (count args)))))
       (interpose ", ")
       (apply str)))

(defn callbacks
  [fns]
  (->> fns
       (partition 2)
       (map (fn [[f args]]
              (str "-callback '" f "'("
                   (apply str
                          (interpose ", " (repeat (count args) "any()")))
                   ") -> any().\n")))
       (apply str)))

(defn type-ordering
  [x]
  (if (underscore? x)
    0
    (-> x
        (primitive-types {:order -1})
        :order)))

(defn module
  [proto-name fns]
  (let [types (->> proto-name
                   utils/who-implements
                   (into ['_])
                   (sort-by type-ordering))]
    (str "-module('" proto-name "').\n\n"
         "-include(\"clojerl.hrl\").\n"
         "-include(\"clojerl_int.hrl\").\n\n"
         "-clojure(true).\n"
         "-protocol(true).\n\n"
         (let [ex (exports fns)]
           (when-not (empty? ex)
             (str "-export([" ex "]).\n")))
         "-export([?SATISFIES/1]).\n\n"
         (callbacks fns)
         "\n"
         (proto-functions proto-name fns types)
         (satisfies?-function types))))

(defn write-module
  [content proto-name]
  (let [path (if (str/starts-with? (str proto-name) "clojerl.")
               "src/erl/lang/protocols/"
               "src/erl/erlang/")]
    (println "Writing" (str path proto-name ".erl"))
    (spit (str path proto-name ".erl") content)))

(defn write-protocols
  [protocols]
  (doseq [[proto-name fns] protocols]
    (-> proto-name
        (module fns)
        ;; println
        (write-module proto-name)
        )))

(defn -main []
  (write-protocols protocols))
